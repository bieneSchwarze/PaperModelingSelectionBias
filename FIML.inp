TITLE: FIML model;

DATA:
  FILE IS dataSC4MatheWIDE.csv;
  FORMAT IS FREE;
  TYPE IS INDIVIDUAL;

VARIABLE:
  NAMES ARE pid iid dgcf sc sex hs rs rest mig amode age math1 math2 drop;
  USEVARIABLES ARE dgcf sc math1 math2;
  MISSING ARE .;
  CLUSTER IS iid;
  WITHIN ARE dgcf sc;
 
ANALYSIS:
   TYPE = TWOLEVEL;
   ESTIMATOR = ML;

MODEL:

  %WITHIN%
  iw BY math1-math2@1;     ! latent intercept 
  sw BY math1@0 math2@1;   ! latent slope
  sw@0;                    ! no random slope
  iw WITH sw@0;            ! no correlation between intercept and slope
  math1-math2 (1);         ! equal residual variance (= mixed-effects model)         
  iw ON dgcf sc;
  sw ON dgcf sc;           ! interaction with time
  [dgcf sc];               ! for FIML

  %BETWEEN%
  ib BY math1-math2@1;     ! latent intercept
  sb BY math1@0 math2@1;   ! latent slope
  sb@0;                    ! (no) random slope
  ib WITH sb@0;            ! no correlation between intercept and slope
  math1-math2@0;           !   
  [ib sb math1@0 math2@0]; ! no item-level intercept, but latent means

OUTPUT: TECH1 CINTERVAL;

 